<A*> -> <PROG> 'fim-de-arquivo';
<PROG> -> 'programa' 'id' 'pvirg' <DECLS> <C-COMP>;
<DECLS> -> & | 'variaveis' <LISTDECLS> ;
<LISTDECLS> -> <DECL-TIPO> <D>;
<D> -> & | <LISTDECLS>;
<DECL-TIPO> -> <LIST-ID> 'dpontos' <TIPO> 'pvirg';
<LIST-ID> -> 'id' <E>;
<E>	-> & | 'virg' <LIST-ID>;
<TIPO> -> 'inteiro' | 'real' | 'logico' | 'caracter';
<C-COMP> -> 'abrech' <LISTA-COMANDOS> 'fechach' ;
<LISTA-COMANDOS> -> <COMANDOS> <G>;
<G> -> & | <LISTA-COMANDOS>;
<COMANDOS> -> <IF> | <WHILE> | <READ> | <WRITE> | <ATRIB>;
<IF> -> 'se' 'abrepar' <EXPR> 'fechapar' <C-COMP> <H> ;
<H> -> & | 'senao' <C-COMP> ;
<WHILE> -> 'enquanto' 'abrepar' <EXPR> 'fechapar' <C-COMP> ;
<READ> -> 'leia' 'abrepar' <LIST-ID> 'fechapar' 'pvirg';
<ATRIB> -> 'id' 'atrib' <EXPR> 'pvirg';
<WRITE> -> 'escreva' 'abrepar' <LIST-W> 'fechapar' 'pvirg';
<LIST-W> -> <ELEM-W> <L>;
<L> -> & | 'virg' <LIST-W>;
<ELEM-W> -> <EXPR> | 'cadeia';
<EXPR> -> <SIMPLES> <P>;
<P> -> & | 'oprel' <SIMPLES>;
<SIMPLES> -> <TERMO> <R>;
<R> -> & | 'opad' <SIMPLES>;
<TERMO> -> <FAT> <S>;
<S> -> & | 'opmul' <TERMO>;
<FAT> -> 'id' | 'cte' | 'abrepar' <EXPR> 'fechapar' | 'verdadeiro' | 'falso' | 'opneg' <FAT>;